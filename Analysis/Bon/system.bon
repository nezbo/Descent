system_chart DESCENT
	cluster MODEL_CLUSTER description "The model of the game parts"
	cluster GUI_CLUSTER description "The visual representation of the game"
	cluster MESSAGING description "TODO: Write description"
	cluster STATE_STUFF description "States, state machine and related stuff"
end

cluster_chart MODEL_CLUSTER
	cluster PLAYER_CLUSTER description "The actual player, and his/her role"
	cluster EVENT_CLUSTER description "Events, effects and abilities"
	cluster BOARD_CLUSTER description "The game board"
end

cluster_chart PLAYER_CLUSTER
	class ROLE description "The role of a player in the game" 
	class PLAYER description "The actual player, playing the game"
	class HERO_PARTY description "A party of heroes"
	class OVERLORD description "The role of an overlord that a player can take"
	cluster FIGURE_CLUSTER description "A figure with basic stats"
	cluster OVERLORD_STUFF description "Stuff that is specific for an overlord"
end

cluster_chart FIGURE_CLUSTER
	class FIGURE description "A figure with basic stats"
	class HERO description "A hero, made from a herosheet"
	class PET description "A pet that a HeroPlayer can control"
	class MONSTER description "A monster, controlled by  the overlord"
	cluster HERO_STUFF description "Everything that is specific to a hero"
end

cluster_chart HERO_STUFF
	class SKILL description "The skill-cards of a hero"
	class INVENTORY description "The inventory of a hero"
	class EQUIPMENT description "Equipment can be worn by the hero"
	class POTION description "A potion, that can either be on a square, or equipped on a Hero"
	class SURGE_ABILITY description "A surge power on a piece of equipment"
	class TRAINING_TOKEN description "A token that give extra dice in a specific kind of attack"
end

cluster_chart OVERLORD_STUFF
	class OVERLORD_CARD description "Cards that the overlord draws each turn"
	class TRAP_CARD description "A type of Overlord_Card to make traps"
	class POWER_CARD description "A type of Overlord_Card to give continuing powers"
	class EVENT_CARD description "A type of Overlord_Card to do special events"
	class SPAWN_CARD description "A type of Overlord_Card to spawn creatures"
end

-- HOW TO DO/DESCRIBE EVENTS AND ABILITIES??!
cluster_chart EVENT_CLUSTER
	class EFFECT description "An Effect that can be on an figure"
	class POISON_EFFECT description "A poison Effect, that has to be healed through"
	class BURN_EFFECT description "A burn Effect that damages a figure"
	class STUN_EFFECT description "A stun Effect, that limits movement and attacks"
	class WEB_EFFECT description "A web Effect that limits movement"
	class ABILITY description "The abilities that a figure can have"
	class EVENT description "A Event that will make something happen"
	class TRIGGERED_EVENT description "An Event that is triggered by something og some ability"
	class PASSIVE_EVENT description "An Event that is always on"
end

cluster_chart BOARD_CLUSTER
	class BOARD description "The entire gameboard, made up of pieces"
	class SQUARE description "A single square of the board"
	class TOWN description "When in town the players can buy, they also go there when they die"
	cluster MARKER_CLUSTER description "Something that can be on a square that is not a figure"
end

cluster_chart MARKER_CLUSTER
	class MARKER description "Something that can be on a square that is not a figure"
	class GLYPH_MARKER description "Allows heroes to teleport to other glyphs or town"
	class MONEY_MARKER description "A gold marker, that can be on a square"
	class RUNE_KEY_MARKER description "A rune that will open a rune-door, that is either on the map, or aquired by the party"
	class TREASURE_CHEST_MARKER description "A treasure chest, that can be placed on a board"
	class OBSTACLE_MARKER description "Either rubble, water or pit, that can be on a square"
end

cluster_chart GUI_CLUSTER
	indexing
		author: "Emil Juul Jacobsen";
		created: "27-11-2011";
		revised: "30-11-2011"
	class DRAWABLE description "Something that should be able to get drawn"
	class GUI description "The overall visual representation of the game"
	class GUI_ELEMENT description "A visual element on the user interface"
	class BOARD_GUI_ELEMENT description "The visualization of the squares of the board"
	class TEXT description "A single line of text"
	class GUI_ELEMENT_FACTORY description "Responsible for creating hierarchies of GUIElements"
	class CHAT description "Lists chat messages and useful game events on the screen"
end

cluster_chart MESSAGING
	indexing
		author: "Simon Westh Henriksen";
		created: "27-11-2011";
		revised: "01-12-2011";
	cluster ASYNC_SOCKETS_CLUSTER description "Implementation of asynchronous TCP sockets server and client."
	cluster CONNECTION_CLUSTER description "Abstraction of the client/server socket connections"
	cluster EVENT_CLUSTER description "Classes related to event parsing and execution"
end

cluster_chart ASYNC_SOCKETS_CLUSTER
	indexing
		author: "Simon Westh Henriksen";
		created: "01-12-2011";
		revised: "01-12-2011";
	class ASYNC_SOCKETS_CLIENT description "Implementation of the asynchronous sockets client."
	class ASYNC_SOCKETS_SERVER description "Implementation of the asynchronous sockets server."
	class CLIENT_INFO description "Stores information about a client connection."
end

cluster_chart CONNECTION_CLUSTER
	indexing
		author: "Simon Westh Henriksen";
		created: "01-12-2011";
		revised: "01-12-2011";
	class CONNECTION description "Abstraction of a connection. Can send text messages."
	class CLIENT_CONNECTION description "A client connection"
	class SERVER_CONNECTION description "A server connection"
end

cluster_chart EVENT_CLUSTER
	indexing
		author: "Simon Westh Henriksen";
		created: "01-12-2011";
		revised: "01-12-2011";
	class EVENT_MANAGER description "Responsible for parsing requests from the network and sending out events to the listeners."
	-- EVENT_TYPE is an enum of all event types and is not modelled in BON.
	cluster EVENT_ARGS description "Arguments for events"
end

cluster_chart EVENT_ARGS
	indexing
		author: "Simon Westh Henriksen";
		created: "01-12-2011";
		revised: "01-12-2011";
	class GAME_EVENT_ARGS description "Implementation of event arguments to use with events."
	-- A lot more classes (around 40) inheriting from GAME_EVENT_ARGS exists, but we chose not to model them in BON.
end

cluster_chart STATE_STUFF
	indexing
		author: "Martin Marcher";
	explanation
		"All classes related to state, such as state machine and the state class."
	class STATE_MANAGER description "The external API for state changes."
	class STATE_MACHINE description "The handler of all states."
	class STATE description "A collection of all different states."
end

class_chart ROLE
	indexing
		author: "Emil Juul Jacobsen";
		in_cluster: "PLAYER_CLUSTER"
		created: "03-12-2011";
		revised: "03-12-2011";
	explanation
		"Describes what Role a given player has at the moment."
	query
		"Can i get this role?"
end

class_chart PLAYER
	indexing
		author: "Jonas Breindahl";
		in_cluster: "PLAYER_CLUSTER"
		created: "21-11-2011";
		revised: "21-11-2011"
	explanation
		"The actual player, playing the game"
	query
		"What is your connection?",
		"What is your role?",
		"Are you client or server?",
		"What is your unique player ID?"
	command
		"Your unique player ID is this!"
end

class_chart HERO_PARTY
	indexing
		author: "Jonas Breindahl";
		in_cluster: "PLAYER_CLUSTER"
		created: "21-11-2011";
		revised: "01-12-2011"
	explanation
		"A party of heroes"
	query
		"How many heroes are in the HeroParty?",
		"How many heroes have yet to take their turn?",
		"Has 'this' heroes yet to take their turn?",
		"How many conquest tokens do the heroes have?",
		"Is the pool of conquest tokens empty?"
	command
		"Add 'this' hero to the list of heroes in the HeroParty!",
		"Remove 'this' hero from the list of heroes yet to act!",
		"Reset list of heroes yet to act!",
		"Remove 'this' number of conquest tokens from the pool of tokens!",
		"Add 'this' number of conquest tokens to the pool of tokens!"
	constraint
		"A HeroParty must always have 2 to 4 heroes."
end

class_chart OVERLORD
	indexing
		author: "Jonas Breindahl";
		in_cluster: "PLAYER_CLUSTER"
		created: "21-11-2011";
		revised: "30-11-2011"
	explanation
		"The role of a overlord, that a hero can be"
	query
		"How many cards do you have?",
		"How many threat tokens do you have?",
		"What cards do you have?",
		"How many cards are in your overlord deck?",
		"How many cards are in your overlord discard?"
	command
		"Draw 'this' many cards!",
		"Shuffle the overlord discard into the deck!"
	constraint
		"When it is not the overlords turn, the overlord cannot have more than 7 cards in hand."
end

class_chart FIGURE
	indexing
		author: "Jonas Breindahl";
		created: "21-11-2011";
		revised: "30-11-2011";
	explanation
		"A figure with basic stats"
	query
		"What is your unique figure ID?",
		"What is your name?",
		"What is your max health?",
		"What is your health right now?",
		"What is your armor?",
		"What is your speed?",
		"How much speed do you have left?",
		"How many attacks do you have left?",
		"What dice do you use to attack?",
		"What abilities do you have?",
		"What effects are on you?"
	command
		"Add 'this' amount of health!",
		"Lose 'this' amount of health!",
	constraint
		"A Figures name must be longer than 0 characters",
		"A Figure must always have more than 0 max health",
		"A Figure may never have less than 0 life or more life than max life",
		"A Figures speed must be more than 0",
		"A Figures armor must be more than or equals to 0",
		"A Figure must always have more than or equal to 0 speed left",
		"A Figure must always have more than or equal to 0 attacks left"
end

class_chart HERO -- A hero, made from a hero-sheet
	indexing
		author: "Jonas Breindahl";
		created: "21-11-2011";
		revised: "05-12-2011"
	explanation 
		"A hero, made from a hero-sheet"
	query
		"How many skills do you have in MELEE/RANGED/MAGIC?",
		"What skills do you have in MELEE/RANGED/MAGIC?",
		"What is your max fatique?",
		"What is your fatique right now?"
	command
		"Add 'this' amount of fatique!",
		"Lose 'this' amount of fatique!",
		"Add 'this' skill!",
		"Remove 'this' skill!"		
	constraint
		"A Hero may never have more fatique than max fatique"	
end

-- THIS IS A BIT IFFY? IS IT A TOKEN OR A FIGURE?
class_chart PET 
	indexing
		author: "Jonas Breindahl";
		created: "21-11-2011";
		revised: "21-11-2011"
	explanation 
		"A pet that a HeroPlayer can control"
	query
		"What hero owns you?",
		"What are your abilities?",
		"What is you max speed?"
end

class_chart MONSTER
	indexing
		author: "Jonas Breindahl";
		created: "05-12-2011";
		revised: "05-12-2011"
	explanation
		"A monster, controlled by the Overlord"
	query
		"Are you a master?"
end

class_chart SKILL
	indexing
		author: "Jonas Breindahl";
		created: "21-11-2011";
		revised: "27-11-2011"
	explanation
		"The skill-cards of a hero"
	query
		"What type of skill are you?",
		"What is your name?",
		"What are your abilities?"
	command
		"Add this ability to your list of ability?"
	constraint
		"A Skill must have atleast one ability",
		"A Skill cannot have more than one type"
end

class_chart INVENTORY
	indexing
		author: "Jonas Breindahl";
		cluster: "HERO_STUFF"
		created: "27-11-2011";
		revised: "27-11-2011";
	explanation
		"The inventory of a hero"
	query
		"How many hands do you have?",
		"What weapons do you have in your hands?",
		"What armor do you have equipped?",
		"What other-items do you have equipped?",
		"What potions do you have equipped?",
		"What equipment do you have in your backpack?",
		"Is there more space in your backpack?"		
	command
		"Equip 'this' weapon!",
		"Equip 'this' armor!",
		"Equip 'this' other item!",
		"Equip 'this' potion!",
		"Put this Equipment in your backpack!",
		"Remove this Equipment from your backpack!"		
	constraint
		"An Inventory can only have 2 other items equipped",
		"An Inventory can only have 3 potions equipped",
		"An Inventory can only have weapons or shields in max the number of hands he has"
end

class_chart EQUIPMENT
	indexing
		author: "Jonas Breindahl";
		created: "21-11-2011";
		revised: "21-11-2011"
	explanation
		"Equipment can be worn by the hero"
	query
		"What is your name?",
		"What type of equipment are you?",
		"What is your price?",
		"How many hands do you use?",
		"Are you tapped right now?",
		"What are your surge abilities?",
		"What are your abilities?",
		"Are you equipped?"
	command
		"Tap/untap!",
		"Equip to this hero!",
		"Unequip from this hero!"
	constraint
		"An equipment may only be tapped one time per round"
end

class_chart SURGE_ABILITY
	indexing
		author: "Jonas Breindahl";
		created: "21-11-2011";
		revised: "21-11-2011"
	explanation
		"A surge power on a piece of equipment"
	query
		"How many surges do you cost?",
		"What ability do you grant?",
end

class_chart POTION
	indexing
		author: "Jonas Breindahl";
		created: "21-11-2011";
		revised: "21-11-2011"
	explanation
		"A potion, that can either be on a square, or equipped on a Hero"
	inherit
		EQUIPMENT
	query
		"What kind of potion are you?"
end

class_chart OVERLORD_CARD
	indexing
		author: "Jonas Breindahl";
		in_cluster: "OVERLORD_STUFF";
		created: "21-11-2011";
		revised: "21-11-2011"
	explanation
		"Cards that the overlord draws each turn"
	query
		"What is your name?",
		"What is your description?",
		"What type of overlord card are you?",
		"What abilities do you have?"
	command
		"Your name is 'this'!",
		"Your description is 'this'!",
		"Add 'this' ability to the list of abilities!"
end

class_chart TRAP_CARD
	indexing
		author: "Jonas Breindahl";
		created: "21-11-2011";
		revised: "21-11-2011"
	explanation
		"A type of Overlord_Card to make traps"
	inherit
		OVERLORD_CARD
end

class_chart POWER_CARD
	indexing
		author: "Jonas Breindahl";
		created: "21-11-2011";
		revised: "21-11-2011"
	explanation
		"A type of Overlord_Card"
	inherit
		OVERLORD_CARD
end

class_chart EVENT_CARD
	indexing
		author: "Jonas Breindahl";
		created: "21-11-2011";
		revised: "21-11-2011"
	explanation
		"A type of Overlord_Card"
	inherit
		OVERLORD_CARD
end

class_chart SPAWN_CARD
	indexing
		author: "Jonas Breindahl";
		created: "21-11-2011";
		revised: "21-11-2011"
	explanation
		"A type of Overlord_Card"
	inherit
		OVERLORD_CARD
	query
		"Can I have a list of all monsters that can be spawned with this card?"
end

class_chart EFFECT
	indexing
		author: "Jonas Breindahl";
		created: "23-11-2011";
		revised: "23-11-2011"
	explanation
		"An effect that can be on an figure"
end

class_chart POISON_EFFECT
	indexing
		author: "Jonas Breindahl";
		created: "23-11-2011";
		revised: "23-11-2011"
	explanation
		"A poison effect, that has to be healed through"
	inherit
		EFFECT
end

class_chart BURN_EFFECT
	indexing
		author: "Jonas Breindahl";
		created: "23-11-2011";
		revised: "23-11-2011"
	explanation
		"A burn effect that damages a figure"
	inherit
		EFFECT
end

class_chart STUN_EFFECT
	indexing
		author: "Jonas Breindahl";
		created: "23-11-2011";
		revised: "23-11-2011"
	explanation
		"A stun effect, that limits movement and attacks"
	inherit
		EFFECT
end

class_chart WEB_EFFECT
	indexing
		author: "Jonas Breindahl";
		created: "23-11-2011";
		revised: "23-11-2011"
	explanation
		"A web effect that limits movement"
	inherit
		EFFECT
end

class_chart ABILITY
	indexing
		author: "Jonas Breindahl";
		in_cluster: "EVENT_CLUSTER";
		created: "23-11-2011";
		revised: "30-11-2011"
	explanation
		"An ability"
	query
		"Can this ability be used now?"
end

class_chart EVENT
	indexing
		author: "Jonas Breindahl";
		created: "23-11-2011";
		revised: "23-11-2011"
	explanation
		"A event that will make something happen"
end

class_chart TRIGGERED_EVENT
	indexing
		author: "Jonas Breindahl";
		created: "23-11-2011";
		revised: "23-11-2011"
	explanation
		"An event that is triggered by something og some ability"
	inherit
		EVENT
end

class_chart PASSIVE_EVENT
	indexing
		author: "Jonas Breindahl";
		created: "23-11-2011";
		revised: "23-11-2011"
	explanation
		"An Event that is always on"
	inherit
		EVENT
end

class_chart BOARD
	indexing
		author: "Jonas Breindahl";
		in_cluster: "BOARD";
		created: "23-11-2011";
		revised: "30-11-2011"
	explanation
		"The entire gameboard, made up of squares"
	query
		"What is your width?",
		"What is your height?",
		"Is the coordinate (x/y) within the board?",
		"Is this square pass-/moveable?",
		"Can the overlord spawn here?",
		"Is there line of sight from 'here' to 'there'?",
		"Is there line of sight from 'here' to 'there', not included monsters?",
		"Is this space visible by the players?"
end

class_chart SQUARE
	indexing
		author: "Jonas Breindahl";
		in_cluster: "BOARD";
		created: "23-11-2011";
		revised: "23-11-2011"
	explanation
		"A single square of the board"
	query
		"What marker is on this square?",
		"What figure is on this square?"
end

class_chart TOWN
	indexing
		author: "Jonas Breindahl";
		in_cluster: "BOARD";
		created: "23-11-2011";
		revised: "23-11-2011"
	explanation
		"When in town the players can buy, they also go there when they die"
	query
		"What heroes are in town right now?"
end

class_chart MARKER
	indexing
		author: "Jonas Breindahl";
		created: "23-11-2011";
		revised: "23-11-2011"
	explanation
		"Something that can be on a square that is not a figure"
end

class_chart MONEY_MARKER
	indexing
		author: "Jonas Breindahl";
		created: "23-11-2011";
		revised: "23-11-2011"
	explanation
		"A gold marker, that can be on a square"
	inherit
		MARKER
end

class_chart RUNE_KEY_MARKER
	indexing
		author: "Jonas Breindahl";
		created: "23-11-2011";
		revised: "23-11-2011"
	explanation
		"A rune that will open a rune-door, that is either on the map, or aquired by the party"
	inherit
		MARKER
	query
		"What color are your?"
end

class_chart TREASURE_CHEST_MARKER
	indexing
		author: "Jonas Breindahl";
		created: "23-11-2011";
		revised: "23-11-2011"
	explanation
		"A treasure chest, that can be placed on a board"
	inherit
		MARKER
	query
		"What kind of treasure do you have?",
		"How many treasures do you have?",
		"How much gold do you have?",
		"How many conquest tokens do you have?",
		"How many curses do you have?"
		
end

class_chart OBSTACLE_MARKER
	indexing
		author: "Jonas Breindahl";
		created: "23-11-2011";
		revised: "23-11-2011"
	explanation
		"Either rubble, water or pit, that can be on a square"
	inherit
		MARKER
end

class_chart DRAWABLE
	indexing
		author: "Emil Juul Jacobsen";
		created: "30-11-2011";
		revised: "30-11-2011";
	explanation
		"Something that should be able to be drawn on the screen"
	query
		"What do you look like?"
end

class_chart GUI
	indexing
		author: "Emil Juul Jacobsen";
		created: "27-11-2011";
		revised: "01-12-2011"
	explanation
		"Handles the visual aspects of the game and input from the user"
	command
		"Update yourself!",
		"Change your state GUI_ELEMENT to this one!",
		"Draw everything!"
end

class_chart GUI_ELEMENT
	indexing
		author: "Emil Juul Jacobsen";
		created: "27-11-2011";
		revised: "01-12-2011";
	explanation
		"A single element of the user interface that, itself, can contain sub-elements"
	query
		"What is your position?",
		"What is your dimensions?",
		"Is this point within your bondaries?",
		"Are you focused?"
	command
		"Handle this mouse click!",
		"Handle this key press!",
		"Do this when clicked",
		"Add this GUI_ELEMENT to your collection of sub-elements!",
		"Add this DRAWABLE to be displayed in you (or your children) at this position!",
		"Add this text to be displayed in you (or your children) at this position!",
		"Draw yourself!"
	constraint
		"The position must be within the window boundaries",
		"The dimensions combined with the position must not go beyond the window boundaries",
		"All sub-elements must be within the boundaries of this GUI_ELEMENT"
end

class_chart BOARD_GUI_ELEMENT
	indexing
		author: "Emil Juul Jacobsen";
		created: "01-12-2011";
		revised: "01-12-2011";
	inherit
		GUI_ELEMENT
	explanation
		"The element on the screen that visualizes the game board."
	command
		"Mark this field as positive or negative!",
		"Clear all marks!"
end

class_chart TEXT
	indexing
		author: "Emil Juul Jacobsen";
		created: "04-12-2011";
		revised: "04-12-2011";
	explanation
		"Information about a text to be displayed on the screen."
	query
		"What text do you contain?",
		"At what position should you be drawn?"
	constraint
		"The text should not exceed the boundaries of it's enclosing GUI_ELEMENT"
end

class_chart GUI_ELEMENT_FACTORY
	indexing
		author: "Emil Juul Jacobsen";
		created: "27-11-2011";
		revised: "30-11-2011";
	explanation
		"Responsible of making hierarchies of GUIElements for two of the layers"
	command
		"Create the GUI_ELEMENT for the visualization of the board!",
		"Create the GUI_ELEMENT for this State from this perspective!",
		"Create the GUI_ELEMENT of the menus, chat and player board for this perspective!"
	constraint
		"The position of the root GUI_ELEMENTS must be 0,0",
		"The width and height of the root GUI_ELEMENT must be the same as the game window"
end

class_chart CHAT
	indexing
		author: "Emil Juul Jacobsen";
		created: "29-11-2011";
		revised: "29-11-2011";
	explanation
		"Handles messages and important game events for presentation on the screen"
	inherit
		GUI_ELEMENT
	command
		"Here is a new message!"
	constraint
		"The message should not be empty"
end

class_chart ASYNC_SOCKETS_CLIENT
	indexing
		author: "Simon Westh Henriksen";
		created: "01-12-2011";
		revised: "01-12-2011";
	explanation
		"Implementation of the asynchronous sockets client."
	query
		"What is your socket?"
	command
		"Give me an instance of you using 'this' address and 'this' port!"
	constraint
		"If the object is returned, the connection has been established successfully."
end

class_chart ASYNC_SOCKETS_SERVER
	indexing
		author: "Simon Westh Henriksen";
		created: "01-12-2011";
		revised: "01-12-2011";
	explanation
		"Implementation of the asynchronous sockets server."
	query
		"What is your local IP?"
	command
		"Give me an instance of you using 'this' port!",
		"Start listening for client connections!",
		"Broadcast 'this' message to all connected clients!",
		"Broadcast 'this' message to all connected clients except the on with 'this' id!",
		"Call 'this' delegate when a client has connected"
end

class_chart CLIENT_INFO
	indexing
		author: "Simon Westh Henriksen";
		created: "01-12-2011";
		revised: "01-12-2011";
	explanation
		"Stores information about a client connection."
	query
		"What is your ID?",
		"What is your local IP?"
	command
		"Give me an instance of you using 'this' socket!",
		"Send 'this' string!",
		"Begin receiving content!",
		"Call 'this' delegate when a message was received!",
		"Call 'this' delegate when the connection was closed!"
	constraint
		"The ID will always be unique in the context of the current game and can be used to identify the player."
end

class_chart CONNECTION
	indexing
		author: "Simon Westh Henriksen";
		created: "28-11-2011";
		revised: "01-12-2011";
	explanation
		"Abstraction of a connection. Can send text messages."
	query
		"What is your connection id?",
		"What is your local ip?"
	command
		"Initiate connection and start receiving data!",
		"Send 'this' string!"
	constraint
		"A connection is always either a server or a client."
end

class_chart CLIENT_CONNECTION
	indexing
		author: "Simon Westh Henriksen";
		created: "28-11-2011";
		revised: "01-12-2011";
	explanation
		"A TCP client connected to the host server. When sending a message, it will send only to the server at the other end of the connection."
	inherit
		CONNECTION
	command
		"Give me an instance of you using 'this' ip and 'this' port!"
end

class_chart SERVER_CONNECTION
	indexing
		author: "Simon Westh Henriksen";
		created: "28-11-2011";
		revised: "01-12-2011";
	explanation
		"A TCP server that accepts client connections. Sends message to all the clients. This type of connection will broadcast received messages to everyone but the sender."
	inherit
		CONNECTION
	command
		"Give me an instance of you using 'this' port!"
end

class_chart EVENT_MANAGER
	indexing
		author: "Simon Westh Henriksen";
		created: "28-11-2011";
		revised: "01-12-2011";
	explanation
		"Takes strings received from the network and fires the appropriate events. Can also convert an event to a text string and send it."
	command
		"Call 'this' method when 'this' event is fired!", -- This command exists for every type of event in our system.
		"Parse 'this' text string and fire the appropriate event!", -- This also includes descion 
		"Queue 'this' event!",
		"Queue 'this' event-string!",
		"Process your event queue!"
	constraint
		"After game initialization, there must always be one event manager."
end

class_chart EVENT_ARGS
	indexing
		author: "Simon Westh Henriksen";
		created: "01-12-2011";
		revised: "01-12-2011";
	explanation
		"A base class containing arguments for events. This base class implements the required event arguments fields."
	query
		"Who (player_id) sent you?",
		"What is your event id"?,
		"What type of event are you?",
		"Do you need a response?"
	command
		"Populate yourself with arguments using this argument array."
end

class_chart STATE_MANAGER
	indexing
		author: "Martin Marcher";
	explanation
		"The handler of all states. Knows about the current state and what to do next."
	query
		"Can I have the unique object of this class?",
		"What is the current state?",
		"What was the previous 'n' states?",
		"What are the next possible state(s)?"
	command
		""
	constraint
		"After game initiation, there must always be a current state",
		"You can only go from 'current state' to a state part of 'current state's next possible states."
end

class_chart STATE_MACHINE
	indexing
		author: "Martin Marcher";
	explanation
		"The handler of all states. Knows about the current state and what to do next."
	query
		"What is the current state?",
		"What is the next state?",
		"What was the previous 'n' states?",
		"Going back, do I find this one before that?"
	command
		"Change to next state!",
		"Place these states after the current state!"
	constraint
		"The StateMachine is only visible to the StateManager.",
		"After game initiation, there must always be a current state",
		"As long as the game runs there is always a next state"
end

class_chart STATE
	indexing
		author: "Martin Marcher";
	explanation
		"A collection of all different states."
	query
		"Can I have this state?"
end

